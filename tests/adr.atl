;ATALAN ADR test
;
;Test working with addresses.
;Setting address to variable, passing array addresses to procedures etc.

const a1:array = 10,11,12,13,14
const a2:array = 20,21,22,23,24

w:array(0..5)
a:adr of array(5)

;Print first two elements from passed array.
;This means, we need at least array with two elements passed.
;This is currently not checked by compiler.

print2:proc x:adr =
   b1 = x(0)
   b2 = x(1)
   "[b1],[b2]"

set:proc x:adr, offset:byte, b:byte =
   x(offset) = b
  
;We may assign address of array to address and later use the address to access
;array elements.

a = a1
b1 = a(2)    ; b1 = a1(2) = 12
a = a2
b2 = a(2)    ; b2 = a2(2) = 22

;We may also set address of some array element.
a = a1(2)		; a = 12,13,14
b3 = a(1)

;"## Array adr"
"[b1],[b2],[b3]"
assert "12,22,13"

;"## Passing array to proc"
print2 a1
assert "10,11"
print2 a2(2)
assert "22,23"

;"## Passing adr to proc"

a = a1
print2 a
assert "10,11"
a = a1(2)
print2 a
assert "12,13"

;"## Modifying adr"

a = w
a(0) = 30
a(1) = 31

print2 w
assert "30,31"

;"## Modifying adr in proc"
set w 2 32
set w 3 33

print2 w(2)
assert "32,33"
